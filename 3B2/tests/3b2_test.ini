::
:: 3B2 exerciser: Run through the "normal" diagnostics on the 3B2/400 diagnostics floppy
::
:: If you get the message:
::
::   **
::   ** 3b2/400 diagnostic failed
::   **
::
:: then, yes, the simulator is probably broken in some way (DGMON's phase 17 is a good
:: example of this.)
::
:: Contributed by B. Scott Michel ("scooter me fecit")
::

cd %~p0

:: Borrowed from the Vax diags:
if ("%1" == "-v") set console notelnet
set runlimit 4 minutes

set cpu 4m
set cpu noidle
set mau enable
at nvram diags/nvram.bin
at tod diags/tod.bin
at ifloppy diags/3B2_Maintenance_Utilities_4-0.img

noexpect
expect     "SYSTEM FAILURE: CONSULT YOUR SYSTEM ADMINISTRATION UTILITIES GUIDE" send "mcp\n"; go
expect -r  "Enter name of program to execute.*:" send "filledt\n"; go
expect     "Enter Load Device Option Number [0 (FD5)]:" send "\n"; go
expect     "FW ERROR 1-06: BOOT FAILURE" goto DIAGFAIL
expect -r  "Enter name of program to execute.*:" send "dgmon\n"; go
expect     "Enter Load Device Option Number [0 (FD5)]:" send "\n"; go
expect     "Did you boot filledt? [y or n] (n)" send "y\n"; go
expect     "DGMON > " send "DGN SBD PH=1-10\n"; go
expect     "DGMON > " send "DGN SBD PH=11\n"; go
expect     "DGMON > " send "DGN SBD PH=16-18\n"; go
expect [3] "SBD 0 (IN SLOT 0) DIAGNOSTICS PASSED\n" goto SUCCESS
expect     "Diagnostic Failed\n" goto DIAGFAIL

boot
:: Script doesn't really execute past here.

:SUCCESS
echof ""
echof "** 3b2/400 diagnostics completed successfully."
exit 0

:DIAGFAIL
echof
echof "**"
echof "** 3b2/400 diagnostic failed!"
echof "**"
exit 1
